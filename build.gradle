plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "com.diffplug.spotless" version "5.8.2"
}

group "org.quiltmc"
version "0.1.0"
archivesBaseName = project.name

repositories {
    mavenCentral()
}

dependencies {
    compileOnlyApi "org.jetbrains:annotations:20.1.0"

    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

jar {
    from ('.') {
        include 'LICENSE'
    }
}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Target java 8
tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    } else {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

spotless {
    java {
        // Use comma separator for openjdk like license headers
        licenseHeaderFile(file("codeformat/HEADER")).yearSeparator(", ")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()

        def ENV = System.getenv()

        if (ENV.MAVEN_URL) {
            repositories.maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}